{
  parserClass='com.vepanimas.intellij.prisma.lang.parser.PrismaParser'

  implements='com.vepanimas.intellij.prisma.lang.psi.PrismaElement'
  extends='com.vepanimas.intellij.prisma.lang.psi.impl.PrismaElementImpl'

  elementTypeHolderClass='com.vepanimas.intellij.prisma.lang.psi.PrismaElementTypes'
  elementTypeClass='com.vepanimas.intellij.prisma.lang.psi.PrismaElementType'
  tokenTypeClass='com.vepanimas.intellij.prisma.lang.psi.PrismaTokenType'

  psiClassPrefix='Prisma'
  psiImplClassSuffix='Impl'
  psiPackage='com.vepanimas.intellij.prisma.lang.psi'
  psiImplPackage='com.vepanimas.intellij.prisma.lang.psi.impl'

  tokens=[
    MODEL           = 'model'
    TYPE            = 'type'
    ENUM            = 'enum'
    GENERATOR       = 'generator'
    DATASOURCE      = 'datasource'

    UNSUPPORTED     = 'Unsupported'

    LBRACE          = '{'
    RBRACE          = '}'
    LPAREN          = '('
    RPAREN          = ')'
    LBRACKET        = '['
    RBRACKET        = ']'
    EQ              = '='
    DOT             = '.'
    COLON           = ':'
    QUEST           = '?'
    EXCL            = '!'
    AT              = '@'
    ATAT            = '@@'
    COMMA           = ','

    STRING_LITERAL  = 'regexp:\"([^\\\"\r\n]|\\[^\r\n])*\"?'
    NUMERIC_LITERAL = 'regexp:-?\d+(\.\d+)?'
    IDENTIFIER      = 'regexp:[a-zA-Z][a-zA-Z0-9\-_]*'
    WHITE_SPACE     = 'regexp:\s+'
  ]

  extends('FunctionCall|ArrayExpression|LiteralExpression|PathExpression')=Expression
  implements('(Model|Type|Enum|Datasource|Generator)Declaration|TypeAlias')='com.vepanimas.intellij.prisma.lang.psi.PrismaDeclaration'

  implements('.*Block')='com.vepanimas.intellij.prisma.lang.psi.PrismaBlock'
  mixin('.*Block')='com.vepanimas.intellij.prisma.lang.psi.impl.PrismaBlockMixin'

  implements('FieldDeclaration|EnumValueDeclaration|KeyValue')='com.vepanimas.intellij.prisma.lang.psi.PrismaMemberDeclaration'
}

Schema ::= (Declaration | ConsumeIllegalIdentifier)*

private Declaration ::=
    ModelDeclaration
    | EnumDeclaration
    | DatasourceDeclaration
    | GeneratorDeclaration
    | TypeAlias
    | TypeDeclaration
    {
        pin=1
        recoverWhile=Declaration_recover
    }
private Declaration_recover ::= !TopLevelKeywords
private TopLevelKeywords ::= MODEL | TYPE | ENUM | GENERATOR | DATASOURCE

ModelDeclaration ::= MODEL Identifier FieldDeclarationBlock {
    mixin="com.vepanimas.intellij.prisma.lang.psi.impl.PrismaEntityDeclarationMixin"
    implements="com.vepanimas.intellij.prisma.lang.psi.PrismaEntityDeclaration"
    pin=1
}
TypeDeclaration ::= TYPE Identifier FieldDeclarationBlock {
    mixin="com.vepanimas.intellij.prisma.lang.psi.impl.PrismaEntityDeclarationMixin"
    implements="com.vepanimas.intellij.prisma.lang.psi.PrismaEntityDeclaration"
    pin=1
}

FieldDeclarationBlock ::= '{' (FieldDeclaration | BlockAttribute)* '}' {
    pin=1
}
FieldDeclaration ::= IDENTIFIER ':'? FieldType? FieldAttribute* {
    mixin="com.vepanimas.intellij.prisma.lang.psi.impl.PrismaFieldDeclarationMixin"
    pin=1
}

EnumDeclaration ::= ENUM Identifier EnumDeclarationBlock {
    mixin="com.vepanimas.intellij.prisma.lang.psi.impl.PrismaEnumDeclarationMixin"
    pin=1
}
EnumDeclarationBlock ::= '{' (EnumValueDeclaration | BlockAttribute)* '}' {
    pin=1
}
EnumValueDeclaration ::= Identifier FieldAttribute* {
    mixin="com.vepanimas.intellij.prisma.lang.psi.impl.PrismaEnumValueDeclarationMixin"
    pin=1
}

DatasourceDeclaration ::= DATASOURCE Identifier KeyValueBlock {
    mixin="com.vepanimas.intellij.prisma.lang.psi.impl.PrismaConfigBlockDeclarationMixin"
    pin=1
}
GeneratorDeclaration ::= GENERATOR Identifier KeyValueBlock {
    mixin="com.vepanimas.intellij.prisma.lang.psi.impl.PrismaConfigBlockDeclarationMixin"
    pin=1
}

KeyValueBlock ::= '{' KeyValue* '}' {
    pin=1
}
KeyValue ::= Identifier '=' Expression {
    mixin="com.vepanimas.intellij.prisma.lang.psi.impl.PrismaKeyValueMixin"
    pin=1
}

TypeAlias ::= TYPE Identifier '=' TypeReference FieldAttribute* {
    mixin="com.vepanimas.intellij.prisma.lang.psi.impl.PrismaTypeAliasMixin"
    pin=3
}

/* Types */
FieldType ::=
    UnsupportedOptionalListType
    | ListType
    | OptionalType
    | LegacyRequiredType
    | LegacyListType
    | TypeReference

UnsupportedType ::= UNSUPPORTED '(' STRING_LITERAL ')' {
    pin=1
    recoverWhile=UnsupportedType_recover
}
private UnsupportedType_recover ::= !(')' | '}' | '@' | '@@' | IDENTIFIER)

TypeReference ::= UnsupportedType | Identifier {
    mixin="com.vepanimas.intellij.prisma.lang.psi.impl.PrismaTypeReferenceMixin"
    implements="com.vepanimas.intellij.prisma.lang.psi.PrismaReferencingElement"
}
UnsupportedOptionalListType ::= TypeReference '[' ']' '?'
ListType ::= TypeReference '[' ']'
OptionalType ::= TypeReference '?'
LegacyRequiredType ::= TypeReference '!'
LegacyListType ::= '[' TypeReference ']'

/* Attributes */
BlockAttribute ::= '@@' Path ArgumentsList? {
    pin=1
}
FieldAttribute ::= '@' Path ArgumentsList? {
    pin=1
}

/* Arguments */
ArgumentsList ::= '(' [Argument (',' Argument)*] ','? ')'
Argument ::= NamedArgument | Expression
NamedArgument ::= Identifier ':' Expression

/* Expressions */
Expression ::=
    FunctionCall
    | ArrayExpression
    | LiteralExpression
    | PathExpression

FunctionCall ::= Path ArgumentsList
ArrayExpression ::= '[' [Expression (',' Expression)*] ']'
LiteralExpression ::= NUMERIC_LITERAL | STRING_LITERAL
PathExpression ::= Path

/* Identifiers */
private Identifier ::= IDENTIFIER
private ConsumeIllegalIdentifier ::= IDENTIFIER {
    consumeTokenMethod="consumeTokenFast"
}
Path ::= Identifier ('.' Path?)*